apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

version '1.1.3'

if (project.parent.name.equals("AlgorigoUtils")) {
    apply plugin: 'com.jfrog.bintray'
    group 'com.algorigo.library'

    publishing {
        publications {
            AlgorigoUtil(MavenPublication) {
                artifact("$buildDir/outputs/aar/algorigoutil-release.aar")
                groupId this.group
                artifactId 'AlgorigoUtil'
                version this.version

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included in the pom.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = project.property("BINTRAY_USER") ?: System.getenv('BINTRAY_USER')
        key = project.property("BINTRAY_KEY") ?: System.getenv('BINTRAY_KEY')
        publications = ['AlgorigoUtil']
        configurations = ['archives']
        override = true
        pkg {
            repo = 'maven'
            name = 'AlgorigoUtil'
            userOrg = 'chagilhwan'
            description = "Algorigo Utility Library"
            publish = true
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            websiteUrl = 'https://github.com/Algorigo/AlgorigoUtils'
            vcsUrl = '${websiteUrl}.git'
            dryRun = false
            version {
                name = this.version
                desc = 'minor update'
                released = new Date()
                vcsTag = this.version
            }
        }
    }
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName this.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        defaultConfig {
            consumerProguardFiles 'lib-proguard-rules.txt'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'

    // Joda time
    implementation 'joda-time:joda-time:2.10.2'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
}

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }
}
